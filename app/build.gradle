def versionNumber = 1
def versionBuildType = 0 // 0 debug 1 release
def keyReleasePassword = ''
def keystoreReleasePassword = ''
def realmDatabaseName = "vocabularycreator.realm";

if (project.hasProperty("versionBuildType")) {
    versionBuildType = Integer.parseInt(project.getProperty("versionBuildType"))
}
if (project.hasProperty("versionNumber")) {
    versionNumber = project.getProperty("versionNumber")
}
if (project.hasProperty("keyReleasePassword")) {
    keyReleasePassword = project.getProperty("keyReleasePassword")
}
if (project.hasProperty("keystoreReleasePassword")) {
    keystoreReleasePassword = project.getProperty("keystoreReleasePassword")
}

ext {
    versionMajor = versionBuildType
    versionMinor = 0
    versionPatch = versionNumber

    supportLibVersion = '25.3.1'
    daggerVersion = '2.11-rc2'
    retrofitVersion = '2.1.0'
    okhttp3Version = '3.4.1'
    butterknifeVersion = '8.6.0'
    espressoVersion = '2.2.2'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'realm-android'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.przemyslawlusnia.vocabularycreator"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode versionNumber.toInteger()

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "REALM_DATABASE_NAME", "\"${realmDatabaseName}\""
    }

    signingConfigs {
        config {
            keyAlias 'vocabularycreator'
            keyPassword keyReleasePassword
            storeFile file('./przemyslawlusnia-keystore.jks')
            storePassword keystoreReleasePassword
        }
    }
    flavorDimensions "developmentStage"
    productFlavors {
        prod {
            dimension "developmentStage"
        }

        develop {
            applicationIdSuffix ".develop"
            dimension "developmentStage"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            minifyEnabled false
            testCoverageEnabled = true // gradlew createDevelopDebugCoverageReport
        }

        release {
            signingConfig signingConfigs.config
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

}

repositories {
    maven {
        url 'https://github.com/uPhyca/stetho-realm/raw/master/maven-repo'
    }
    maven {
        url 'https://github.com/linchaolong/stetho-realm/raw/master/maven-repo'
    }
    mavenCentral()
}

// for kapt
kapt {
    generateStubs = true
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    kapt "com.google.dagger:dagger-compiler:${daggerVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation 'javax.annotation:jsr250-api:1.0' // used in Dagger generated classes

    implementation "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    implementation "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    implementation "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okhttp3Version}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp3Version}"
    implementation 'io.reactivex:rxjava:1.2.3'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.facebook.stetho:stetho:1.4.2'
    implementation 'com.uphyca:stetho_realm:2.0.1'
    implementation 'com.jakewharton.timber:timber:4.5.1'
    implementation 'com.google.firebase:firebase-crash:10.2.6'

    androidTestCompile("com.android.support.test.espresso:espresso-core:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.squareup', module: 'javawriter'
    })
    androidTestCompile "com.android.support:support-annotations:${supportLibVersion}"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    // Allows to work with RecyclerView, DatePicker, Drawer, Accessibility, CountingIdlingResource
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:${espressoVersion}", {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }

    implementation "com.android.support:appcompat-v7:${supportLibVersion}"
    implementation "com.android.support:design:${supportLibVersion}"
    implementation "com.android.support:cardview-v7:${supportLibVersion}"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.29'
    androidTestCompile 'org.mockito:mockito-core:2.2.29'
    androidTestCompile 'com.linkedin.dexmaker:dexmaker-mockito:2.2.0'
    // 2.2.0 is newer, but I don't need Mockito 2.x yet (2.2.0 implicitly add Mockito 2.x library).

    androidTestCompile "com.google.dagger:dagger:${daggerVersion}"
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
}

// ADD THIS AT THE BOTTOM
// for google-services.json file configuration usage
apply plugin: 'com.google.gms.google-services'

task checkStyle(type: Checkstyle) {
    configFile = file("../config/checkstyle/checkstyle.xml")
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    ignoreFailures = false
    classpath = files()
}
